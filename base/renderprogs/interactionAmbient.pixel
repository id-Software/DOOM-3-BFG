/*
===========================================================================

Doom 3 BFG Edition GPL Source Code
Copyright (C) 1993-2012 id Software LLC, a ZeniMax Media company. 

This file is part of the Doom 3 BFG Edition GPL Source Code ("Doom 3 BFG Edition Source Code").  

Doom 3 BFG Edition Source Code is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

Doom 3 BFG Edition Source Code is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with Doom 3 BFG Edition Source Code.  If not, see <http://www.gnu.org/licenses/>.

In addition, the Doom 3 BFG Edition Source Code is also subject to certain additional terms. You should have received a copy of these additional terms immediately following the terms and conditions of the GNU General Public License which accompanied the Doom 3 BFG Edition Source Code.  If not, please request a copy in writing from id Software at the address below.

If you have questions concerning this license or the applicable additional terms, you may contact in writing id Software LLC, c/o ZeniMax Media Inc., Suite 120, Rockville, Maryland 20850 USA.

===========================================================================
*/

#include "global.inc"

uniform Texture2D<float4>	samp0 : register(t0); // texture 1 is the per-surface bump map
uniform Texture2D<float4>	samp1 : register(t1); // texture 2 is the light falloff texture
uniform Texture2D<float4>	samp2 : register(t2); // texture 3 is the light projection texture
uniform Texture2D<float4>	samp3 : register(t3); // texture 4 is the per-surface diffuse map
uniform Texture2D<float4>	samp4 : register(t4); // texture 5 is the per-surface specular map

struct PS_IN {
	half4 position	: SV_POSITION;
	half4 texcoord0	: TEXCOORD0_centroid;
	half4 texcoord1	: TEXCOORD1_centroid;
	half4 texcoord2	: TEXCOORD2_centroid;
	half4 texcoord3	: TEXCOORD3_centroid;
	half4 texcoord4	: TEXCOORD4_centroid;
	half4 texcoord5	: TEXCOORD5_centroid;
	half4 texcoord6	: TEXCOORD6_centroid;
	half4 color		: COLOR0;
};

struct PS_OUT {
	half4 color : SV_TARGET;
};

void main( PS_IN fragment, out PS_OUT result ) {
	half4 bumpMap =			samp0.Sample(baseSampler, fragment.texcoord1.xy );
	half4 lightFalloff =	idtex2Dproj( samp1, fragment.texcoord2 );
	half4 lightProj	=		idtex2Dproj( samp2, fragment.texcoord3 );
	half4 YCoCG =			samp3.Sample(baseSampler, fragment.texcoord4.xy );
	half4 specMap =			samp4.Sample(baseSampler, fragment.texcoord5.xy );

	const half3 ambientLightVector = half3( 0.5f, 9.5f - 0.385f, 0.8925f );
	half3 lightVector = normalize( ambientLightVector );
	half3 diffuseMap = ConvertYCoCgToRGB( YCoCG );

	half3 localNormal;
	localNormal.xy = bumpMap.wy - 0.5;
	localNormal.z = sqrt( abs( dot( localNormal.xy, localNormal.xy ) - 0.25 ) );
	localNormal = normalize( localNormal );

	const half specularPower = 10.0f;
	half hDotN = dot3( normalize( fragment.texcoord6.xyz ), localNormal );
	half3 specularContribution = _half3( pow( hDotN, specularPower ) );

	half3 diffuseColor = diffuseMap * sceneConstants.rpDiffuseModifier.xyz;
	half3 specularColor = specMap.xyz * specularContribution * sceneConstants.rpSpecularModifier.xyz;
	half3 lightColor = dot3( lightVector, localNormal ) * lightProj.xyz * lightFalloff.xyz;

	result.color.xyz = ( diffuseColor + specularColor ) * lightColor * fragment.color.xyz;
	result.color.w = 1.0;
}
